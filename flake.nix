# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.19)
{

  # Flake inputs
  inputs = {
    baton.url = "github:conductorOne/baton";
    pre-commit-hooks.url = "github:cachix/git-hooks.nix";
    flake-schemas.url = "https://flakehub.com/f/DeterminateSystems/flake-schemas/*";

    nixpkgs.url = "https://flakehub.com/f/NixOS/nixpkgs/*";
  };

  # Flake outputs that other flakes can use
  outputs =
    {
      self,
      flake-schemas,
      nixpkgs,
      pre-commit-hooks,
      baton,
    }:
    let
      # Helpers for producing system-specific outputs
      supportedSystems = [
        "x86_64-linux"
        "aarch64-darwin"
        "x86_64-darwin"
        "aarch64-linux"
      ];
      forEachSupportedSystem =
        f:
        nixpkgs.lib.genAttrs supportedSystems (
          system:
          f {
            pkgs = import nixpkgs { inherit system; };
          }
        );

      ignoreFolders = [ "vendor" ];
    in
    {
      # Schemas tell Nix about the structure of your flake's outputs
      schemas = flake-schemas.schemas;

      # Development environments
      devShells = forEachSupportedSystem (
        { pkgs }:
        {
          default = pkgs.mkShell {
            # Pinned packages available in the environment
            packages = with pkgs; [
              go
              nil

              gomodifytags
              panicparse

              self.packages.${pkgs.system}.default
              baton.packages.${pkgs.system}.default
            ];
          };
        }
      );

      packages = forEachSupportedSystem (
        { pkgs }:
        {
          default = pkgs.buildGoModule {
            pname = "baton-greenhouse";
            version = "0.1.0";

            src = ./.;

            modSha256 = "sha256-hs9KwPP+JXB7UqFoWiaBvPd/Hq8tRGGwWFo7W7RdU9w=";
            vendorHash = null;

            meta = with pkgs.lib; {
              description = "Baton connector for Greenhouse";
              license = licenses.asl20;
            };
          };
        }
      );

      checks = forEachSupportedSystem (
        { pkgs }:
        {
          pre-commit-check = pre-commit-hooks.lib.${pkgs.system}.run {
            src = ./.;
            hooks = {
              nil = {
                enable = true;
                excludes = ignoreFolders;
              };
              golangci-lint = {
                enable = true;
                excludes = ignoreFolders;
              };
              gotest = {
                enable = true;
                excludes = ignoreFolders;
              };
            };
          };
        }
      );
    };
}
